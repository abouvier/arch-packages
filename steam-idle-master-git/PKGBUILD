#!/bin/bash
# shellcheck disable=SC2034,SC2164
# Maintainer: Alexandre Bouvier <contact at amb dot tf>
_pkgname=idle-master
pkgname="steam-$_pkgname-git"
pkgver=r14.d14fd81
pkgrel=2
pkgdesc="Get your Steam Trading Cards the easy way"
arch=('any')
url="http://www.steamidlemaster.com/"
license=('GPL')
depends=(
	'steam'
	'unionfs-fuse'
	'python2-beautifulsoup4'
	'python2-requests'
	'python2-pillow'
	'tk'
	'python2-colorama'
)
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(
	"$pkgname::git+https://github.com/jshackles/idle_master_py.git"
	"$_pkgname.sh"
)
md5sums=('SKIP'{,})

pkgver() {
	cd "$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$pkgname"
	# shellcheck disable=SC2154
	install -Dm755 "$srcdir/$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname"
	install -Dm644 -t "$pkgdir/usr/share/$_pkgname" Releases/Linux/*.{py,so}
	install -Dm644 -t "$pkgdir/usr/share/doc/$_pkgname" Documentation/*.pdf
}
