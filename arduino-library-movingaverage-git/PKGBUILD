#!/bin/bash
# shellcheck disable=SC2034,SC2164
# Maintainer: Alexandre Bouvier <contact at amb dot tf>
_pkgname=MovingAverage
pkgname="arduino-library-${_pkgname,,}-git"
pkgver=0.1.1.r3.g2b80f57
pkgrel=3
pkgdesc="Moving average library for Arduino"
arch=('any')
url="https://github.com/abouvier/$_pkgname"
license=('Apache')
depends=('arduino-library-circularbuffer')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("$pkgname::git+https://github.com/abouvier/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed 's/^v//;s/-/.r/;s/-/./'
}

package() {
	cd "$pkgname"
	# shellcheck disable=SC2154
	install -Dm644 -t "$pkgdir/usr/share/arduino/libraries/$_pkgname/src" src/*
	install -Dm644 -t "$pkgdir/usr/share/arduino/libraries/$_pkgname" *.*
}
