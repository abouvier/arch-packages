# Maintainer: Alexandre Bouvier <contact@amb.tf>
# shellcheck shell=bash disable=SC2034
pkgname=foobar2000-encoders-bin
_pkgver=2020-04-17
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Encoder binaries for the Converter component"
arch=('any')
url="https://www.foobar2000.org/encoderpack"
license=('BSD' 'GPL' 'LGPL')
makedepends=('wget' 'p7zip')
optdepends=('foobar2000>=1.1.14')
source=("Free_Encoder_Pack-$_pkgver.exe::$url")
md5sums=('c9b01b3fd301dc970b9aefe960151fdb')

# bypass dynamic download link
DLAGENTS=('https::/usr/bin/wget -rl2 -nH --cut-dirs=3 -A exe')

prepare() {
	7z x "Free_Encoder_Pack-$_pkgver.exe"
}

package() {
	# shellcheck disable=SC2154
	install -Dm755 -t "$pkgdir"/usr/share/foobar2000/encoders \
		{flac,lame,mpcenc,oggenc2,opusenc,refalac,wavpack}.exe
	install -Dm644 -t "$pkgdir"/usr/share/licenses/foobar2000 \
		{opusenc,wavpack}_license.txt
}
