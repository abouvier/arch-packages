# Maintainer: Alexandre Bouvier <contact@amb.tf>
# shellcheck shell=bash disable=SC2034
pkgname=foobar2000-encoders-bin
_pkgver=2019-08-04
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Encoder binaries for the Converter component"
arch=('any')
url="https://www.foobar2000.org/encoderpack"
license=('BSD' 'GPL' 'LGPL')
depends=('foobar2000>=1.1.14')
makedepends=('wget' 'p7zip')
source=("Free_Encoder_Pack-$_pkgver.exe::$url")
md5sums=('84e3ff572b272aa1853e024e3f2e1b2e')

# bypass dynamic download link
DLAGENTS=('https::/usr/bin/wget -rl2 -nH --cut-dirs=3 -A exe')

prepare() {
	7z x "Free_Encoder_Pack-$_pkgver.exe"
}

package() {
	# shellcheck disable=SC2154
	install -Dm755 -t "$pkgdir"/usr/share/foobar2000/encoders \
		{flac,lame,mpcenc,oggenc2,opusenc,refalac,wavpack}.exe
	install -Dm644 -t "$pkgdir"/usr/share/licenses/foobar2000 \
		{opusenc,wavpack}_license.txt
}
