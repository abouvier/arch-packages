#!/bin/bash
# shellcheck disable=SC2034,SC2164
# Maintainer: Alexandre Bouvier <contact at amb dot tf>
_pkgname=foobar2000
pkgname="$_pkgname-bin"
pkgver=1.3.10
pkgrel=4
pkgdesc="Advanced freeware audio player for the Windows platform"
arch=('any')
url="https://www.foobar2000.org/"
license=('custom')
depends=('wine' 'unionfs-fuse' 'shared-mime-info' 'gtk-update-icon-cache' 'desktop-file-utils')
makedepends=('p7zip' 'icoutils')
optdepends=(
	"$_pkgname-playcount-bin: playback statistics support"
	"$_pkgname-texttools-bin: custom copy to clipboard support"
)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=(
	"$_pkgname-$pkgver.exe::https://www.dropbox.com/s/6bwn5b056ibzfeg/foobar2000_v$pkgver.exe"
	"$_pkgname.desktop"
	"$_pkgname.xml"
	"$_pkgname.sh"
)
md5sums=(
	'2994486afdcce7f78d80019a797a8ab2'
	'SKIP'
	'SKIP'
	'SKIP'
)

prepare() {
	7z x -y "$_pkgname-$pkgver.exe"
	icotool -xi7 icons/*.ico 2>/dev/null
	wrestool -xRo. foobar2000.exe
}

package() {
	# shellcheck disable=SC2154
	install -Dm755 "$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname"
	install -Dm755 -t "$pkgdir/usr/share/$_pkgname" foobar2000.exe
	install -Dm644 -t "$pkgdir/usr/share/$_pkgname" ./*.{dll,html,css}
	install -Dm644 -t "$pkgdir/usr/share/$_pkgname/themes" themes/*.fth
	install -Dm644 -t "$pkgdir/usr/share/$_pkgname/components" components/*.dll
	install -Dm644 fpl_7_256x256x32.png "$pkgdir/usr/share/icons/hicolor/256x256/mimetypes/application-x-fb2k-playlist.png"
	install -Dm644 generic_7_256x256x32.png "$pkgdir/usr/share/icons/hicolor/256x256/mimetypes/application-x-fb2k-component.png"
	install -Dm644 fth_7_256x256x32.png "$pkgdir/usr/share/icons/hicolor/256x256/mimetypes/application-x-fb2k-theme.png"
	install -Dm644 foobar2000.exe_PNG_269 "$pkgdir/usr/share/icons/hicolor/256x256/apps/$_pkgname.png"
	install -Dm644 foobar2000.exe_TEXT_225 "$pkgdir/usr/share/licenses/$_pkgname/license.html"
	install -Dm755 -t "$pkgdir/usr/share/applications" "$_pkgname.desktop"
	install -Dm644 -t "$pkgdir/usr/share/mime/packages" "$_pkgname.xml"
	touch "$pkgdir/usr/share/$_pkgname/portable_mode_enabled"
}
