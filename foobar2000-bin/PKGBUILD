# Maintainer: abouvier <abouvier at student.42 dot fr>
pkgname=foobar2000-bin
pkgver=1.3.10
pkgrel=1
pkgdesc="Advanced freeware audio player for the Windows platform"
arch=('any')
url="https://www.foobar2000.org/"
license=('custom')
depends=('hicolor-icon-theme' 'xdg-utils' 'shared-mime-info' 'wine' 'unionfs-fuse')
makedepends=('p7zip' 'icoutils')
optdepends=('foobar2000-playcount-bin: playback statistics support')
install="$pkgname.install"
source=(
	"$pkgname-$pkgver.exe::http://www.foobar2000.org/files/f0dd25a16a8b697c02a4a194b548ea3a/foobar2000_v$pkgver.exe"
	"$pkgname.desktop"
	"$pkgname.xml"
	"$pkgname.sh"
)
md5sums=(
	'e391acfb267f50e785d3f618368699bd'
	'SKIP'
	'SKIP'
	'SKIP'
)

prepare() {
	7z x \
		-x!'foobar2000 Shell Associations Updater.exe' \
		-x!'[LICENSE].rtf' \
		-x!'[NSIS].nsi' \
		-x!'$PLUGINSDIR' \
		-x!'$R0' \
		-x!'uninstall.exe' \
		-o"$pkgname-$pkgver" "$pkgname-$pkgver.exe"
	cd "$pkgname-$pkgver"
	find -type d -exec chmod 755 "{}" +
	wrestool -xRn225 -o "$srcdir/license.html" foobar2000.exe
	wrestool -xRn269 -o "$srcdir/${pkgname%-bin}.png" foobar2000.exe
	icotool -xi7 -o "$srcdir/application-x-fb2k-playlist.png" icons/fpl.ico
	icotool -xi7 -o "$srcdir/application-x-fb2k-component.png" icons/generic.ico
	icotool -xi7 -o "$srcdir/application-x-fb2k-theme.png" icons/fth.ico
	touch portable_mode_enabled
	rm -rf icons
}

package() {
	install -d "$pkgdir/usr/share"
	cp -R "$pkgname-$pkgver" "$pkgdir/usr/share/${pkgname%-bin}"
	install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/${pkgname%-bin}"
	install -Dm644 "$pkgname.xml" "$pkgdir/usr/share/mime/packages/${pkgname%-bin}.xml"
	install -Dm755 "$pkgname.desktop" "$pkgdir/usr/share/applications/${pkgname%-bin}.desktop"
	install -Dm644 -t "$pkgdir/usr/share/icons/hicolor/256x256/mimetypes" application-x-fb2k-*.png
	install -Dm644 -t "$pkgdir/usr/share/icons/hicolor/256x256/apps" "${pkgname%-bin}.png"
	install -Dm644 -t "$pkgdir/usr/share/licenses/${pkgname%-bin}" license.html
}
