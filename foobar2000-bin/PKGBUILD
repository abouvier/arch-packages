#!/bin/bash
# shellcheck disable=SC2034,SC2164
# Maintainer: Alexandre Bouvier <contact at amb dot tf>
_pkgname=foobar2000
pkgname="$_pkgname-bin"
pkgver=1.3.10
pkgrel=2
pkgdesc="Advanced freeware audio player for the Windows platform"
arch=('any')
url="https://www.foobar2000.org/"
license=('custom')
depends=('wine' 'unionfs-fuse' 'shared-mime-info' 'gtk-update-icon-cache' 'desktop-file-utils')
makedepends=('p7zip' 'icoutils')
optdepends=(
	"$_pkgname-playcount-bin: playback statistics support"
	"$_pkgname-texttools-bin: custom copy to clipboard support"
)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=(
	"$_pkgname-$pkgver.exe::https://www.dropbox.com/s/6bwn5b056ibzfeg/foobar2000_v$pkgver.exe"
	"$_pkgname.desktop"
	"$_pkgname.xml"
	"$_pkgname.sh"
)
md5sums=(
	'2994486afdcce7f78d80019a797a8ab2'
	'SKIP'
	'SKIP'
	'SKIP'
)

prepare() {
	7z x \
		-x!'foobar2000 Shell Associations Updater.exe' \
		-x!'[LICENSE].rtf' \
		-x!'[NSIS].nsi' \
		-x!'$PLUGINSDIR' \
		-x!'$R0' \
		-x!'uninstall.exe' \
		-o"$_pkgname-$pkgver" "$_pkgname-$pkgver.exe"
	cd "$_pkgname-$pkgver"
	find -type d -exec chmod 755 "{}" +
	wrestool -xRn225 -o "$srcdir/license.html" foobar2000.exe
	wrestool -xRn269 -o "$srcdir/$_pkgname.png" foobar2000.exe
	icotool -xi7 -o "$srcdir/application-x-fb2k-playlist.png" icons/fpl.ico
	icotool -xi7 -o "$srcdir/application-x-fb2k-component.png" icons/generic.ico
	icotool -xi7 -o "$srcdir/application-x-fb2k-theme.png" icons/fth.ico
	touch portable_mode_enabled
	rm -rf icons
}

package() {
	install -d "$pkgdir/usr/share"
	cp -R "$_pkgname-$pkgver" "$pkgdir/usr/share/$_pkgname"
	install -Dm755 "$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname"
	install -Dm644 "$_pkgname.xml" "$pkgdir/usr/share/mime/packages/$_pkgname.xml"
	install -Dm755 "$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
	install -Dm644 -t "$pkgdir/usr/share/icons/hicolor/256x256/mimetypes" application-x-fb2k-*.png
	install -Dm644 -t "$pkgdir/usr/share/icons/hicolor/256x256/apps" "$_pkgname.png"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$_pkgname" license.html
}
